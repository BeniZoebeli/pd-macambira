;
;
; This file is a template with packages/win32_inno/Makefile turns
; into a proper .iss ISTool/Inno Setup file.  <hans@at.or.at>
;
;

[Setup]
AppName=pd
AppVerName=Pd-PD_VERSION
AppPublisher=pure-data.org
AppPublisherURL=http://puredata.org
AppSupportURL=http://puredata.org/community/lists/
AppUpdatesURL=http://puredata.org/downloads/
DefaultDirName={pf}\pd
DefaultGroupName=Pure Data
LicenseFile=..\..\externals\maxlib\LICENSE

Compression=zip/9
ChangesAssociations=true
OutputBaseFilename=PACKAGE_NAME
[Tasks]
; NOTE: The following entry contains English phrases ("Create a desktop icon" and "Additional icons"). You are free to translate them into another language if required.
Name: desktopicon; Description: Create a &desktop icon; GroupDescription: Additional icons:
; NOTE: The following entry contains English phrases ("Create a Quick Launch icon" and "Additional icons"). You are free to translate them into another language if required.
Name: quicklaunchicon; Description: Create a &Quick Launch icon; GroupDescription: Additional icons:; Flags: unchecked

[INI]
Filename: {app}\pd.url; Section: InternetShortcut; Key: URL; String: http://puredata.org

[Icons]
Name: {group}\Pure Data; Filename: {app}\bin\pd.exe; IconFilename: {app}\lib\pd.ico; IconIndex: 0; WorkingDir: {app}
Name: {group}\puredata.org; Filename: {app}\pd.url
Name: {group}\Uninstall Pure Data; Filename: {uninstallexe}
Name: {userdesktop}\Pure Data; Filename: {app}\bin\pd.exe; Tasks: desktopicon; IconFilename: {app}\lib\pd.ico; IconIndex: 0; WorkingDir: {app}
Name: {userappdata}\Microsoft\Internet Explorer\Quick Launch\Pure Data; Filename: {app}\bin\pd.exe; Tasks: quicklaunchicon; WorkingDir: {app}; IconFilename: {app}\lib\pd.ico; IconIndex: 0
Name: {group}\Documentation\Pd Manual; Filename: {app}\doc\1.manual\index.htm
Name: {group}\Documentation\Pd Drums Tutorial; Filename: {app}\doc\tutorials\footils\pddrums\pddrums.html
Name: {group}\Documentation\Gem Manual; Filename: {app}\doc\gem\00.manual\index.html
Name: {group}\Documentation\Gem Primer; Filename: {app}\doc\gem\GemPrimer.pdf
;-----------------------------------------------------------------------------
; GRIPD
#ifexist "build\gripd\gripd.exe"
Name: {group}\GrIPD; Filename: {app}\gripd\gripd.exe
#endif


[Run]
; NOTE: The following entry contains an English phrase ("Launch"). You are free to translate it into another language if required.
Filename: {app}\bin\pd.exe; Description: Launch Pure Data; Flags: nowait postinstall skipifsilent

[UninstallDelete]
Type: files; Name: {app}\pd.url

[Registry]
Root: HKCR; SubKey: Pure.Data; ValueType: dword; ValueName: EditFlags; ValueData: $00000000; Flags: noerror uninsdeletekey
Root: HKCR; SubKey: Pure.Data; ValueType: dword; ValueName: BrowserFlags; ValueData: $00000008; Flags: uninsdeletekey noerror
Root: HKCR; SubKey: Pure.Data; ValueType: string; ValueData: Pure Data; Flags: uninsdeletekey noerror
Root: HKCR; SubKey: Pure.Data\DefaultIcon; ValueType: string; ValueData: {app}\lib\pdpatch.ico,0; Flags: createvalueifdoesntexist noerror
Root: HKCR; SubKey: Pure.Data\shell; ValueType: string; ValueData: open; Flags: noerror uninsdeletekey
Root: HKCR; SubKey: Pure.Data\shell\open\command; ValueType: string; ValueData: """{app}\bin\pd.exe"" -listdev ""%1"""; Flags: uninsdeletekey noerror
Root: HKCR; SubKey: .pd; ValueType: string; ValueData: Pure.Data; Flags: noerror uninsdeletekey
Root: HKCR; SubKey: .pd; ValueType: string; ValueName: Content Type; ValueData: text/plain; Flags: noerror uninsdeletekey

[_ISTool]
Use7zip=false
OutputExeFilename=Output\PACKAGE_NAME.exe

[Files]
; commented out lines have no files, so they give an error
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
;
;
;-----------------------------------------------------------------------------
; packages\win32_inno
Source: pdpatch.ico; DestDir: {app}\lib
Source: pd.ico; DestDir: {app}\lib
;
;
;-----------------------------------------------------------------------------
; put pdsend and pdreceive in the System dir so that they are in the path
;
Source: build\bin\pdsend.exe; DestDir: {sys}; Flags: confirmoverwrite promptifolder
Source: build\bin\pdreceive.exe; DestDir: {sys}; Flags: confirmoverwrite promptifolder
; add Cyclone's cyclist.exe if it is present
#ifexist "build\bin\cyclist.exe"
Source: build\bin\cyclist.exe; DestDir: {sys}; Flags: confirmoverwrite promptifolder
#endif
;
;
;-----------------------------------------------------------------------------
; MinGW Files
; these are some helpful utilities from MinGW
;
; start.exe - like Mac OS X's 'open' command, allows you to open files and
; URLs from the command line
;Source: C:\msys\1.0\bin\start.exe; DestDir: {sys}; Flags: confirmoverwrite
;
;
;-----------------------------------------------------------------------------
; build system files
;
;Source: build\doc\manuals\Pd\Welcome.html; DestDir: {app}; Flags: isreadme; Tasks: 
Source: build\doc\manuals\Pd\ReadMe.html; DestDir: {app}; Flags: isreadme
;Source: build\doc\manuals\Pd\License.html; DestDir: {app}; Flags: isreadme
;Source: build\doc\manuals\Pd\Pd-LICENSE.txt; DestDir: {app}; Flags: isreadme
Source: build\pd-settings.reg; DestDir: {app}; Flags: ignoreversion
Source: build\bin\*.*; DestDir: {app}\bin; Flags: ignoreversion confirmoverwrite recursesubdirs uninsremovereadonly
Source: build\doc\*.*; DestDir: {app}\doc; Flags: ignoreversion confirmoverwrite recursesubdirs uninsremovereadonly; Attribs: readonly
Source: build\extra\*.*; DestDir: {app}\extra; Flags: ignoreversion recursesubdirs uninsremovereadonly promptifolder; Attribs: readonly
Source: build\lib\*.*; DestDir: {app}\lib; Flags: ignoreversion recursesubdirs
Source: build\gripd\*.*; DestDir: {app}\gripd; Flags: ignoreversion
;; build stuff is not included now
;Source: build\portaudio\*.*; DestDir: {app}\portaudio; Flags: ignoreversion recursesubdirs
;Source: build\src\*.*; DestDir: {app}\src; Flags: ignoreversion recursesubdirs
;Source: build\tcl\*.*; DestDir: {app}\tcl; Flags: ignoreversion recursesubdirs
